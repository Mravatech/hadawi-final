plugins {
    id "com.android.application"

    // Firebase configuration
    id "com.google.gms.google-services"

    // Kotlin
    id "kotlin-android"

    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.app.hadawi_app"
    ndkVersion = flutter.ndkVersion
    compileSdk = 35

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    defaultConfig {
        applicationId = "com.app.hadawi_app"
        minSdk = 24
        targetSdk = 35
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.release
            crunchPngs false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation fileTree(include: ["*.aar"], dir: "libs")

    // Kotlin and Coroutines
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.0.21"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1"

    // AndroidX dependencies
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.fragment:fragment-ktx:1.6.2"
    implementation "androidx.webkit:webkit:1.9.0"
    implementation "androidx.browser:browser:1.7.0"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"

    // Google Play Services
    implementation "com.google.android.gms:play-services-wallet:19.2.1"

    // Material Design
    implementation "com.google.android.material:material:1.11.0"

    // JSON Parsing
    implementation "com.google.code.gson:gson:2.10"

    // Firebase (باستخدام BOM لضمان تطابق الإصدارات)
    implementation platform("com.google.firebase:firebase-bom:33.1.2")
    implementation "com.google.firebase:firebase-auth"
    implementation "com.google.firebase:firebase-analytics"
    implementation 'androidx.multidex:multidex:2.0.1'

    // In-App Purchases
    implementation "com.android.billingclient:billing:7.0.0"

    // Core Library Desugaring
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
}